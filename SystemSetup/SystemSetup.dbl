;;**********************************************************************
;;
;; Title:       Program.dbl
;;
;; Type:        Program
;;
;; Description: Program to create and popualte sample data files.
;;
;; Author(s):	Gordon Ireland, Synergy/DE Consultants, Synergex
;;				Richard C. Morris, Technology Evangelist, Synergex.
;;
;; Copyright (c) 2016, Synergex International, Inc. All rights reserved.
;;
;; Redistribution and use in source and binary forms, with or without
;; modification, are permitted provided that the following conditions are met:
;;
;; * Redistributions of source code must retain the above copyright notice,
;;   this list of conditions and the following disclaimer.
;;
;; * Redistributions in binary form must reproduce the above copyright notice,
;;   this list of conditions and the following disclaimer in the documentation
;;   and/or other materials provided with the distribution.
;;
;; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
;; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
;; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
;; ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
;; LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
;; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
;; SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
;; INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
;; CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
;; ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
;; POSSIBILITY OF SUCH DAMAGE.
;;
;;*****************************************************************************

import System
import System.Collections.Generic
import System.Text

;;imports required to use data objects and file IO classes.
import AppData
import Symphony.Conductor.DataIO

namespace SystemSetup
	
	;;; <summary>
	;;; This program creates and populates the files requird for the Symphony Framework Example application
	;;; </summary>  
	main
		
	;		.include 'product' repository, record = 'product'
	;		.include 'product_group' repository, record = 'product_group'
	;		.include 'parameter' repository, record = 'parameter'
		
	record
		ttChn		,i4		,0
		ans			,a1
		;			dataChan	,i4	,0
	endrecord
		
	proc
		;;************************************************************
		;;Becuase we are now using Symphony Threading capabilties we
		;;need to ensure that we correctly "anchor" the process
		;;to the main UI thread
		;;************************************************************
		Symphony.Conductor.Static.ThreadSafe.AnchorToUIThread()

		open(ttChn, I, 'TT:')

		writes(ttChn, 'Recreate all files ?  (Y/N) ?')
		reads(ttChn, ans)
		if (ans == 'Y' .or. ans == 'y')
		begin
			writes(ttChn, 'Creating Files...')
			
			call create
			
			call setParameter
			call loadProduct
			call loadSupplier
			call loadGroup
			call loadBuyer
			
			writes(ttChn, 'Completed')
		end
			
		close ttChn
		flags(7000000, 1)
		stop
		;----------------------------------------------------------------------------------------
		;create files
		
	create,

	data hadError		,boolean		,false

		;;create the folder if it's not there
	data logValue		,a255
	data logLength		,i4
		getlog("SFEDATA", logValue, logLength)
		if (!logLength)
		begin
			writes(ttChn, "Logical SFEDATA not correctly defined")
			return
		end

		if (!System.IO.Directory.Exists(%atrim(logValue)))
		begin
			System.IO.Directory.CreateDirectory(%atrim(logValue))
		end

		writes(ttChn, 'Creating files')
		
		try
		begin
			delet('SFEDATA:product.ism')
		data headerKeyVals		,[2]a*
			&					,"START=1,LENGTH=6,TYPE=ALPHA,NODUPS"
			&	,"START=80:1,LENGTH=3:6,TYPE=ALPHA:ALPHA,DUPS,MODIFY"
			isamc('SFEDATA:product.ism', ^size(STRProduct), 2, headerKeyVals)	
		end
		catch (e, @Exception)
		begin
			writes(ttChn, 'Error creating product.ism file: ' + e.Message)
			hadError = true
		end
		endtry
		
		try
		begin
			delet('SFEDATA:supplier.ism')
			isamc('SFEDATA:supplier.ism', ^size(STRSupplier), 1, "START=1,LENGTH=6,NODUPS")	
		end
		catch (e, @Exception)
		begin
			writes(ttChn,'Error creating supplier.ism file: ' + e.Message)
			hadError = true
		end
		endtry
		
		try
		begin
			delet('SFEDATA:product_group.ism')
			isamc('SFEDATA:product_group.ism', ^size(STRProduct_group), 1, "START=1,LENGTH=3,NODUPS")	
		end
		catch (e, @Exception)
		begin
			writes(ttChn, 'Error creating productgroup.ism file: ' + e.Message)
			hadError = true
		end
		endtry
		
		try
		begin
			delet('SFEDATA:order_header.ism')
		data headerKeyVals		,[4]a*
			&					,"START=1,LENGTH=6,TYPE=DECIMAL,NODUPS"
			&	,"START=7:1,LENGTH=6:6,TYPE=ALPHA:DECIMAL,NODUPS"
			&	,"START=13:1,LENGTH=8:6,TYPE=DECIMAL:DECIMAL,NODUPS"
			&	,"START=21:1,LENGTH=8:6,TYPE=DECIMAL:DECIMAL,NODUPS"
			isamc('SFEDATA:order_header.ism', ^size(STROrder_header), 4, headerKeyVals)	
		end
		catch (e, @Exception)
		begin
			writes(ttChn, 'Error creating OrderHeader.ism file: ' + e.Message)
			hadError = true
		end
		endtry
		
		try
		begin
			delet('SFEDATA:order_line.ism')
		data lineKeyVals	,[2]a*
			&				,"START=1:6,LENGTH=5:6,TYPE=DECIMAL:DECIMAL,NODUPS"
			&	,"START=12:1:6,LENGTH=10:5:6,TYPE=ALPHA:DECIMAL:DECIMAL,NODUPS"
			isamc('SFEDATA:order_line.ism', ^size(STROrder_line), 2, lineKeyVals)	
		end
		catch (e, @Exception)
		begin
			writes(ttChn,'Error creating OrderLine.ism file: ' + e.Message)
			hadError = true
		end
		endtry

		try
		begin
			delet('SFEDATA:buyer.ism')
			data lineKeyVals	,[3]a*
			&	,"START=1,LENGTH=32,TYPE=ALPHA,NODUPS"
			&	,"START=33,LENGTH=8,TYPE=CTIMESTAMP,NODUPS"
			&	,"START=41,LENGTH=8,TYPE=TIMESTAMP,NODUPS"
			isamc('SFEDATA:buyer.ism', ^size(STRBuyer), 3, lineKeyVals)	
		end
		catch (e, @Exception)
		begin
			writes(ttChn,'Error creating Buyer.ism file: ' + e.Message)
			hadError = true
		end
		endtry
		
		if (hadError)
		begin
			writes(ttChn,'Press <RETURN> to continue')
			reads(ttChn, ans)
		end
		return
		
		;--------------------------------------------------------------------
		;set parameter file
		
	setparameter,
		writes(ttChn,'Creating parameter file')

		;;need to create a relative file
		try
		begin
		data tmpChan	,i4		,0
			open(tmpChan, O:R, 'SFEDATA:parameter.dat', RECSIZ:600)
			close(tmpChan)
		end
		catch (e,@Exception)
		begin
			writes(ttChn,'Error creating and setting parameter.dat file: ' + e.Message)		
		end
		endtry

	data parameterIO = new Parameter_fileIO(FileOpenMode.UpdateRelative)

	data paramObj		,@Parameter_Data		,new Parameter_Data()

		;;read the first record (record number 1).  We ignore any "not found" errors.
		parameterIO.ReadRecord(paramObj, 1)

		;;set values		
		paramObj.Last_order_no = 1

		;;update record #1
		if (parameterIO.UpdateRecord(paramObj) != FileAccessResults.Success)
		begin
			writes(ttChn,'Error creating and setting parameter.dat file: ' + %string(parameterIO.LastSynergyError))
		end

		writes(ttChn, "Parameter record created.")
		writes(ttChn, "Press <RETURN> to continue")
		reads(ttChn, ans)
		
		return
		
		
		;------------------------------------------------------------
		; populate product file
	loadproduct,

	data productIO = new Product_FileIO(FileOpenMode.UpdateIndexed)
		
	data product	,STRproduct

		product.str_prod_code			=	'COK001'
		product.str_prod_description	=	'Coke Cans'
		product.str_sell_price			=	8.00	
		product.str_cost_price			=	6.00
		product.str_prod_group	=	'SD'
		product.str_supp_code	=	'D00001'
		product.str_vat_code			=	1
		product.str_pack_size			=	'330ML'
		call addproduct

		product.str_prod_code			=	'COK002'
		product.str_prod_description	=	'Coke 1ltr bottles'
		product.str_sell_price			=	12.00	
		product.str_cost_price			=	10.00
		product.str_prod_group	=	'SD'
		product.str_supp_code	=	'D00001'
		product.str_vat_code			=	1
		product.str_pack_size			=	'12 x 1ltr'
		call addproduct
		
		product.str_prod_code			=	'COK003'
		product.str_prod_description	=	'Coke 2Ltr Bottles'
		product.str_sell_price			=	14.00	
		product.str_cost_price			=	10.00
		product.str_prod_group	=	'SD'
		product.str_supp_code	=	'D00001'
		product.str_vat_code			=	1
		product.str_pack_size			=	'6 x 2Ltr'
		call addproduct
		
		product.str_prod_code			=	'COK004'
		product.str_prod_description	=	'Diet Coke Cans'
		product.str_sell_price			=	8.00	
		product.str_cost_price			=	6.00
		product.str_prod_group	=	'SD'
		product.str_supp_code	=	'D00001'
		product.str_vat_code			=	1
		product.str_pack_size			=	'330ML'
		call addproduct
		
		product.str_prod_code			=	'COK005'
		product.str_prod_description	=	'Diet Coke 1ltr bottle'
		product.str_sell_price			=	12.00	
		product.str_cost_price			=	10.00
		product.str_prod_group	=	'SD'
		product.str_supp_code	=	'D00001'
		product.str_vat_code			=	1
		product.str_pack_size			=	'12 x 1ltr'
		call addproduct
		
		product.str_prod_code			=	'COK006'
		product.str_prod_description	=	'Diet Coke 2Ltr Bottles'
		product.str_sell_price			=	14.00	
		product.str_cost_price			=	10.00
		product.str_prod_group	=	'SD'
		product.str_supp_code	=	'D00001'
		product.str_vat_code			=	1
		product.str_pack_size			=	'6 x 2Ltr'
		call addproduct		
		
		product.str_prod_code			=	'7UP001'
		product.str_prod_description	=	'7UP Cans'
		product.str_sell_price			=	8.50	
		product.str_cost_price			=	11.00
		product.str_prod_group	=	'SD'
		product.str_supp_code	=	'D00001'
		product.str_vat_code			=	1
		product.str_pack_size			=	'330ML'
		product.str_promotion			= 1
		call addproduct
		
		product.str_prod_code			=	'7UP002'
		product.str_prod_description	=	'7UP 1 Litre Bottles'
		product.str_sell_price			=	12.00	
		product.str_cost_price			=	9.00
		product.str_prod_group	=	'SD'
		product.str_supp_code	=	'D00001'
		product.str_vat_code			=	1
		product.str_pack_size			=	'12 x 1 Ltr'
		call addproduct
		
		product.str_prod_code			=	'7UP003'
		product.str_prod_description	=	'7UP 2 Litre Bottles'
		product.str_sell_price			=	13.00	
		product.str_cost_price			=	10.50
		product.str_prod_group	=	'SD'
		product.str_supp_code	=	'D00001'
		product.str_vat_code			=	1
		product.str_pack_size			=	'6 x 2 Ltr'
		product.str_promotion			= 1
		call addproduct
		
		product.str_prod_code			=	'CRISP1'
		product.str_prod_description	=	'Ready Salted Crisps'
		product.str_sell_price			=	6.00	
		product.str_cost_price			=	4.00
		product.str_prod_group	=	'CR'
		product.str_supp_code	=	'C00001'
		product.str_vat_code			=	1
		product.str_pack_size			=	'Box of 48'
		call addproduct
		;gi - I got my duiplicate key error here, as expected
		
		product.str_prod_code			=	'CRISP2'
		product.str_prod_description	=	'Salt and Vinegar Crisps'
		product.str_sell_price			=	6.00	
		product.str_cost_price			=	4.00
		product.str_prod_group	=	'CR'
		product.str_supp_code	=	'C00001'
		product.str_vat_code			=	1
		product.str_pack_size			=	'Box of 48'
		call addproduct
		
		product.str_prod_code			=	'CRISP3'
		product.str_prod_description	=	'Cheese and Onion Crisps'
		product.str_sell_price			=	6.00	
		product.str_cost_price			=	4.00
		product.str_prod_group	=	'CR'
		product.str_supp_code	=	'C00001'
		product.str_vat_code			=	1
		product.str_pack_size			=	'Box of 48'

		call addproduct

	data productObject = new Product_Data()
		
	data productStatus		,FileAccessResults		,productIO.ReadFirstRecord(productObject)
		while (productStatus == FileAccessResults.Success)
		begin
			writes(ttChn, productObject.prod_code + " : " + productObject.prod_description)

			productStatus = productIO.ReadNextRecord(productObject)

		end

		productIO.CloseChannel()

		writes(ttChn, "Press <RETURN> to continue")
		reads(ttChn, ans)

		return

		;------------------------------------------------------------------------------------
	addproduct,
		if (productIO.CreateRecord(new Product_Data(product)) != FileAccessResults.Success)
			writes(ttChn, 'Error adding product record for ' + product.str_prod_code + ' : ' + productIO.LastSynergyError.ToString())
		return


		;; just to keep old code for comparison

		;		writes(ttChn, 'Loading products')
		;		open(dataChan = 0, U:I, 'SFEDATA:product.ism')
		;		
		;		prod_code			=	'COK001'
		;		prod_description	=	'Coke Cans'
		;		sell_price			=	8.00	
		;		cost_price			=	6.00
		;		product.str_prod_group	=	'SD'
		;		product.str_supp_code	=	'D00001'
		;		vat_code			=	1
		;		pack_size			=	'330ML'
		;		call addproduct
		;		
		;
		;		return
		;		
		;--------------------------------------------------------
		;		
		;	addproduct,
		;		try
		;		begin
		;			store(dataChan, product)
		;		end
		;		catch (e,@Exception)
		;		begin
		;			writes(ttChn, 'Error adding product record for ' + prod_code + ' : ' + e.Message)
		;		end
		;		endtry
		;		return
		;		


		;-----------------------------------------------------------------------------
		; populate supplier file
	loadsupplier,
		writes(ttChn, 'loading suppliers')

		data supplierIO = new Supplier_FileIO(FileOpenMode.UpdateIndexed)
		; gi - so this creates an object which has the file open in update mode
			
		data supplier		,STRSupplier

		data currentDate	,a20		,%datetime

		; gi - i use a structure instead fo a record to hold the data I want to add to the file
		supplier.str_supp_code			=	'D00001'
		supplier.str_name				=	'Drinks Company'
		supplier.str_address1			=	'123 Main Road'
		supplier.str_address2			=	'Anytown'
		supplier.str_address3			=
		supplier.str_postcode			=	'UK1 7TH'
		supplier.str_phone				=	'028 91123765'
		supplier.str_contact			=	'Mr T Jones'
		supplier.str_email				=	'Tjones@drinksco.com'
		supplier.str_create_date		=	currentDate(1:8)
		supplier.str_account_status		=   1

		call addsupplier
		
		supplier.str_supp_code	=	'C00001'
		supplier.str_name				=	'Crisp Company'
		supplier.str_address1			=	'456 Main Road'
		supplier.str_address2			=	'Othertown'
		supplier.str_address3			=
		supplier.str_postcode			=	'UK1 1TS'
		supplier.str_phone				=	'0108 6726155'
		supplier.str_contact			=	'Mr B Moore'
		supplier.str_email				=	'BMoore@crisps.com'
		supplier.str_create_date		=	currentDate(1:8)
		supplier.str_account_status		=   2

		call addsupplier

		supplier.str_supp_code			=	'F00001'
		supplier.str_name				=	'Fruits Company'
		supplier.str_address1			=	'1 Buckingham Palace Rd'
		supplier.str_address2			=	'Warminster'
		supplier.str_address3			=	'London'
		supplier.str_postcode			=	'SW 1A 1AA'
		supplier.str_phone				=	'303 1237300'
		supplier.str_contact			=	'Mr P Mountbatten'
		supplier.str_email				=	'PMount@fruitsBP.com'
		supplier.str_create_date		=	currentDate(1:8)
		supplier.str_account_status		=   1

		call addsupplier

		;;let's display the records to the screen

		writes(ttChn, "Suppliers created:")
		
	data supplierObject = new Supplier_Data()
		
	data status		,FileAccessResults		,supplierIO.ReadFirstRecord(supplierObject)
		; gi,  this defines the status and calls the "read first record" for the file class and returns a supplier data object and sets the status we just defined
		;;correct!
		while (status == FileAccessResults.Success)
		begin
			writes(ttChn, supplierObject.Name + " : " + supplierObject.Contact)
			; we can now access properties of the supplier object.  how would we set them and update the file ?

			;; rcm - if we wanted to update the file.....
		data storeName		,string		,supplierObject.Name
			;; rcm - set the "new data"
			supplierObject.Name = "New Name"
			;; rcm - update the file
			if (supplierIO.UpdateRecord(supplierObject) != FileAccessResults.Success)
			begin
				;;we have an error
				writes(ttChn, "Error updating record : " + %string(supplierIO.LastSynergyError))
			end

			status = supplierIO.ReadNextRecord(supplierObject)
			; gi - this then calls a method of the file class to read next record and return as an object ?
			;; rcm - correct!
		end

		;re-display them with updated names
		status = supplierIO.ReadFirstRecord(supplierObject)
		while (status == FileAccessResults.Success)
		begin
			writes(ttChn, supplierObject.Name + " : " + supplierObject.Contact)
			status = supplierIO.ReadNextRecord(supplierObject)
		end
		
		;; rcm - in the loops about we are reading and then updating records - so the records are locked.
		;; rcm - we can also update a record without having it locked - you must have read it first :)
		;; rcm - when the FileIO objects are used we store the GRFA and so can use this.  The GRFA
		;; rcm - is stored in the data object.
		;; rcm - let's just re-read the first 2 records; as - lets make it 3 :)
	data sup1		,@Supplier_Data		,new Supplier_Data()
		supplierIO.ReadFirstRecord(sup1)
	data sup2		,@Supplier_Data		,new Supplier_Data()
		supplierIO.ReadNextRecord(sup2)
		supplierIO.UnlockChannel()	;;force no records to be locked!!
	data sup3		,@Supplier_Data		,new Supplier_Data()
		supplierIO.ReadNextRecord(sup3)
		supplierIO.UnlockChannel()	;;force no records to be locked!!

		;; rcm - update supplier named back to what they were
		sup1.Name = 'Crisp Company'
		sup2.Name = 'Drinks Company'
		sup3.Name = 'Fruits Company'
		
		;; rcm - update supplier 1
		supplierIO.UpdateRecordUsingGRFA(sup1)
		;; rcm - and supplier 2
		supplierIO.UpdateRecordUsingGRFA(sup2)
		;; as - and supplier 3
		supplierIO.UpdateRecordUsingGRFA(sup3)

		;; rcm - re-display them with updated names
		status = supplierIO.ReadFirstRecord(supplierObject)
		while (status == FileAccessResults.Success)
		begin
			writes(ttChn, supplierObject.Name + " : " + supplierObject.Contact)
			status = supplierIO.ReadNextRecord(supplierObject)
		end

		;;close the channel
		supplierIO.CloseChannel()
		; gi - this closes the file.  Does this dispose of the file object as well ? If not, should we dispose of it ?
		; how would we dispose of thedata object ?


		;; rcm - the object will go out of scope so no need to worry about disposing of them here.

		writes(ttChn, "Press <RETURN> to continue")
		reads(ttChn, ans)		

		return
		
		;---------------------------------------------------------
	addsupplier,
		; gi - this creates an object from the structure and passes it to the file obejct.  It has a return value which can be checked

		if (supplierIO.CreateRecord(new Supplier_Data(supplier)) != FileAccessResults.Success)
		
				writes(ttChn, 'Error adding supplier record for ' + supplier.str_supp_code + ' : ' + supplierIO.LastSynergyError.ToString())
		; gi - this replaces the try / catch logic, and uses properties of the data obejct and teh file object for the error message
		; gi - Q - does this object exist after it is created ?  If so, do we need to dispose of it ?

		;; rcm - the object will go out of scope.

		return
		
		;-------------------------------------------------------------------------------
		; populate product group file
	loadgroup,

	data product_groupIO = new product_group_FileIO(FileOpenMode.UpdateIndexed)
		
	data product_group		,STRProduct_group
		
		writes(ttChn, 'loading groups')


		product_group.str_prod_group	= 'SD'
		product_group.str_group_desc	= 'Soft Drinks'
		call addgroup
		
		product_group.str_prod_group	= 'CR'
		product_group.str_group_desc	= 'Crisps'
		call addgroup
		
		product_group.str_prod_group	= 'NUT'
		product_group.str_group_desc	= 'Nuts'
		call addgroup
		
		product_group.str_prod_group	= 'CHO'
		product_group.str_group_desc	= 'Chocolate'
		call addgroup

	data product_groupObject = new Product_group_Data()
		
	data product_groupStatus	,FileAccessResults		,Product_groupIO.ReadFirstRecord(Product_groupObject)
		while (product_groupStatus == FileAccessResults.Success)
		begin
			writes(ttChn, product_groupObject.prod_group + " : " + product_groupObject.group_desc)
			
			product_groupStatus = product_groupIO.ReadNextRecord(product_groupObject)
			
		end

		product_groupIO.CloseChannel()

		writes(ttChn, "Press <RETURN> to continue")
		reads(ttChn, ans)

		return
		;---------------------------------------------------------
	addgroup,
		if (product_groupIO.CreateRecord(new Product_group_Data(product_group)) != FileAccessResults.Success)
			writes(ttChn, 'Error adding product group record for ' + product_group.str_prod_group + ' : ' + product_groupIO.LastSynergyError.ToString())
		return

		;-----------------------------------------------------------------------------
		; populate Buyer file
	loadBuyer,
		writes(ttChn, 'loading Buyers')

		; as- create an object which has the file open in update mode
		data BuyerIO = new Buyer_FileIO(FileOpenMode.UpdateIndexed)
		data Buyer		,STRBuyer
				

		; as - populate fields in the structure 
		Buyer.str_buyer_key			=	Guid.NewGuid().ToString()
		Buyer.str_name				=	'Sherlock Holmes'
		Buyer.str_address1			=	'221B Baker Street'
		Buyer.str_address2			=	'Westminster'
		Buyer.str_address3			=	'London'
		Buyer.str_postcode			=	'NW1'
		Buyer.str_phone				=	'044 2072243688'
		Buyer.str_mobile_phone		=	'044 2072243689'
		Buyer.str_email				=	'holmes@SHPD.com'
		
		call addBuyer
		
		Buyer.str_buyer_key			=	Guid.NewGuid().ToString()
		Buyer.str_name				=	'Richard Starkey'
		Buyer.str_address1			=	'3 Abbey Road'
		Buyer.str_address2			=	"St. John's Wood"
		Buyer.str_address3			=	'London'
		Buyer.str_postcode			=	'NW8 9AY'
		Buyer.str_phone				=	'044 2072667000'
		Buyer.str_mobile_phone		=	
		Buyer.str_email				=	'ringOH@fab1.com'

		call addBuyer

		Buyer.str_buyer_key			=	Guid.NewGuid().ToString()
		Buyer.str_name				=	'Henry Tudor'
		Buyer.str_address1			=	'1 Hampton Court'
		Buyer.str_address2			=	'Surrey'
		Buyer.str_address3			=	
		Buyer.str_postcode			=	'KT8 9AU'
		Buyer.str_phone				=	'044 2031666000'
		Buyer.str_mobile_phone		=	'044 2031666001'
		Buyer.str_email				=	'3C2A1J@sixWives.com'

		call addBuyer

		;; display the records to the screen
		writes(ttChn, "Buyers created:")

		data BuyerObject = new Buyer_Data()
		data buyStatus		,FileAccessResults		,BuyerIO.ReadFirstRecord(BuyerObject)
		
		while (buyStatus == FileAccessResults.Success)
		begin
			writes(ttChn, BuyerObject.Name + ",  " + BuyerObject.Email)
			
			buyStatus = BuyerIO.ReadNextRecord(BuyerObject)
		end
		
		;;close the channel
		BuyerIO.CloseChannel()
		
		writes(ttChn, "Press <RETURN> to continue")
		reads(ttChn, ans)		

		return
		
		;---------------------------------------------------------
	addBuyer,
		; as - create an object from the structure and pass it to the file obejct. Display Error if applicable 
		if (BuyerIO.CreateRecord(new Buyer_Data(Buyer)) != FileAccessResults.Success)
			writes(ttChn, 'Error adding Buyer record for ' + Buyer.str_name + ' : ' + BuyerIO.LastSynergyError.ToString())
		
		return
		
		
		
	endmain
	
endnamespace
